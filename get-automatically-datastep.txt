/* With this code, you can automatically search and get a dataset name, reference and last updated. */

%macro getDataset(
lib_name /* local that your dataset is allocated */,
base_name /* Prefix of dataset you are looking for */,
var /* macro variable that you will save the table name */,
num /* table reference, 1 will mean the newest table, 2 the second newest table and so on */,
dt_ini/* the position that date starts on the table name - 0 means that the table have no date on it */,
dt_total /* Put how many chars have the date string, example 202111 -> 6 */);

    %global &var. DT_&var. DATE_MOD_&var. whatIf;

    proc contents data=&lib_name.._ALL_ nods;
        ods output members=work.base;
    run;

 
    %if &dt_ini. ne 0 %then %do;
    	%let whatIf=1;
        data &var._treated (rename= Name = base_name); set base;
            if find(Name,"&base_name.","i");

            if missing(Num) then delete;
        run;

        proc sort data=&var._treated; by descending base_name ;run;

        DATA _NULL_; SET &var._treated;
            IF _N_ = &num. THEN DO;
                CALL SYMPUT ("&var.", "&lib_name.."||base_name);
                CALL SYMPUT ("DATE_MOD_&var.", substr(PUT(LastModified,NLDATMS27.),4,3)||substr(PUT(LastModified,NLDATMS27.),1,3)||substr(PUT(LastModified,NLDATMS27.),7,4));
                CALL SYMPUT ("DT_&var.", substr(base_name,&dt_ini.,&dt_total.));
            end;
        RUN;

	%end;
	%else %do;
		%let whatIf=2;
	    data &var._treated (rename= Name = base_name); 
			set base;
            if Name = "&base_name.";
        run;

        proc sort data=&var._treated; by descending base_name ;run;

        DATA _NULL_; SET &var._treated;
            IF _N_ = 1 THEN DO;
                CALL SYMPUT ("&var.", "&lib_name.."||base_name);
                CALL SYMPUT ("DATE_MOD_&var.", substr(PUT(LastModified,NLDATMS27.),4,3)||substr(PUT(LastModified,NLDATMS27.),1,3)||substr(PUT(LastModified,NLDATMS27.),7,4));
                CALL SYMPUT ("DT_&var.", 'NoDate');
            end;
        RUN;
	%end;

    options nosource;
    %put Whatif:&whatIf. / &var. - LastModf: &&DATE_MOD_&var.. - Datastep Date: &&DT_&var..;
    options source;

	proc delete data=&var._treated;run;
%mend getDataset;
